req "examples/lib/ansi.dk" as ansi;

use ansi::cursor;
use ansi::display;
use ansi::text;
use std::console;
use std::math;
use std::ps::thread;
use std::str;
use text::color;

let $NUM_ROWS = 25;
let $NUM_COLUMNS = 30;
let $ITERATION_LIMIT = 1000;

fn main() => nil {
  let SLEEP_TIME = 10.0 / 125.0;

  let fb = new_buff();

  seed_buffer(fb);

  let loops = 0;

  # run
  loop {
    display::clear();
    cursor::home();

    display_buffer(fb);

    if !is_population_alive(fb) {
      break;
    }

    let bb = new_buff();

    update(fb, bb);

    if no_change(fb, bb) {
      break;
    }

    fb = bb;

    thread::sleep(SLEEP_TIME);
    loops += 1;

    if loops > $ITERATION_LIMIT {
      break;
    }
  }
}

fn new_buff() => [[nil; $NUM_COLUMNS]; $NUM_ROWS] {
  let b = [nil; $NUM_ROWS];

  for let i = 0; i < $NUM_ROWS; i += 1 {
    b[i] = [nil; $NUM_COLUMNS];
  }

  ret b;
}

fn seed_buffer(buff) => nil {
  for let r = 0; r < $NUM_ROWS; r += 1 {
    for let c = 0; c < $NUM_COLUMNS; c += 1 {
      buff[r][c] = math::abs(math::rand_i32()) % 2;
    }
  }
}

fn display_buffer(buff) => nil {
  let frame = [row_line()];
  for let r = 0; r < $NUM_ROWS; r += 1 {
    let row_parts = [":"];
    for let c = 0; c < $NUM_COLUMNS; c += 1 {
      let v = buff[r][c];

      let cell_color;
      match v {
        0 => cell_color = color::fmt::paint(color::FOREGROUND, color::RGB(56, 56, 56)),
        1 => cell_color = color::fmt::fg(color::CYAN),
      }

      row_parts.push(str::concat("  ", cell_color, v, text::fmt::reset(), "  :"));
    }
    frame.push(row_parts.join(nil));
    frame.push(row_line());
  }
  console::write(frame.join(nil));
  console::writeln();
}

fn row_line() => str {
  let parts = [" -----"; $NUM_COLUMNS];
  ret str::concat("\n", parts.join(nil), "\n");
}

fn update(fb, bb) => nil {
  for let r = 0; r < $NUM_ROWS; r += 1 {
    for let c = 0; c < $NUM_COLUMNS; c += 1 {
      let neighbor_live_cell = count_live_neighbor_cells(fb, r, c);
      if fb[r][c] == 1 and (neighbor_live_cell == 2 or neighbor_live_cell == 3) {
        bb[r][c] = 1;
      } else if fb[r][c] == 0 and neighbor_live_cell == 3 {
        bb[r][c] = 1;
      } else {
        bb[r][c] = 0;
      }
    }
  }
}

fn count_live_neighbor_cells(buff, row, col) => i32 {
  let count = 0;
  for let r = row - 1; r <= row + 1; r += 1 {
    if r < 0 or r >= $NUM_ROWS {
      cont;
    }

    for let c = col - 1; c <= col + 1; c += 1 {
      if r == row and c == col {
        cont;
      }

      if c < 0 or c >= $NUM_COLUMNS {
        cont;
      }

      if (buff[r][c] == 1) {
        count += 1;
      }
    }
  }
  ret count;
}

fn is_population_alive(buff) => bool {
  for let r = 0; r < $NUM_ROWS; r += 1 {
    for let c = 0; c < $NUM_COLUMNS; c += 1 {
      if buff[r][c] == 1 {
        ret true;
      }
    }
  }
  ret false;
}

fn no_change(a, b) => bool {
  for let r = 0; r < $NUM_ROWS; r += 1 {
    for let c = 0; c < $NUM_COLUMNS; c += 1 {
      if a[r][c] != b[r][c] {
        ret false;
      }
    }
  }
  ret true;
}

export main();
